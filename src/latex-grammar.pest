// Top level rule is `document`.
document = { 
    SOI ~
    (section? ~ newline_char)* ~ section? ~
    EOI
} //-- the last section? catches the last line

// section is representing a paragraph
section = {
    env_stmt |
    cmd_stmt |
    expression
}

expression   = { ( cmd_stmt | literal_group )* }
literal_group      = { char+ } //-- letters and numbers with no spaces
char         = @{ ASCII_ALPHANUMERIC | punctuation }
punctuation  = {
    "," | "." | ";" | "(" | ")" | "[" | "]" | "|" | "<" | ">" | ":" 
}

// Control Statement Grammar
cmd_stmt = { ctrl_character ~ (!("begin"|"end") ~ name) ~ cmd_stmt_opt? ~ "{" ~ expression ~ "}" }
cmd_stmt_opt = { "[" ~ name ~ "]" }


// Control Characters & Meta Characters
name                    = @{ ASCII_ALPHA+ } //-- used for command and environment names
COMMENT                 = _{ "%" ~ (!newline_char ~ ANY)* ~ newline_char }
WHITESPACE              = _{ " " }
newline_char            = _{"\n"}
ctrl_character       = _{ "\\" }

// Environment Grammar
env_stmt     = { ctrl_character ~ "begin" ~ "{" ~ PUSH(name) ~ "}" ~ env_content ~ ctrl_character ~ "end" ~ "{" ~ POP ~ "}" }
env_content  = { (section? ~ newline_char)* }
