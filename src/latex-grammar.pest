// Top level rule is `document`.
document = { 
    SOI ~
    (section? ~ newline_char)* ~ section? ~
    EOI
} //-- the last section? catches the last line

section = {
    env_stmt |
    cmd_stmt |
    expression
}

expression   = { ( cmd_stmt | literal )* }
literal      = @{ char+ } //-- letters and numbers with no spaces
char         = @{ ASCII_ALPHANUMERIC | punctuation }
punctuation  = {
    "," | "." | ";" | "(" | ")" | "[" | "]" | "|" | "<" | ">" | ":" 
}

// Control Statement Grammar
cmd_stmt = { ctrl_character ~ name ~ cmd_stmt_opt? ~ "{" ~ expression ~ "}" }
cmd_stmt_opt = { "[" ~ name ~ "]" }


// Control Characters & Meta Characters
name                    = @{ ASCII_ALPHA+ } //-- used for command and environment names
COMMENT                 = _{ "%" ~ (!newline_char ~ ANY)* ~ newline_char }
WHITESPACE              = _{ " " }
newline_char            = _{"\n"}
ctrl_character       = _{ "\\" }

// Environment Grammar
env_stmt     = { env_begin ~ env_content ~ env_end }
env_content  = { (section? ~ newline_char)* }
env_begin    = @{ ctrl_character ~ "begin" ~ "{" ~ PUSH(name) ~ "}" }
env_end      = @{ ctrl_character ~ "end" ~ "{" ~ PEEK ~ "}" }