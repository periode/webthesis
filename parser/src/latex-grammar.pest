// Top level rule is `document`.
document = { 
    SOI ~
    (paragraph? ~ newline_char)* ~ paragraph? ~
    EOI
} //-- the last paragraph? catches the last line

// basic unit
paragraph = {
    code_stmt | env_stmt |
    ( cmd_stmt | literal_group )*
}

//-- letters and numbers with no spaces, brackets are included separately to address options
literal_group      = { (char | "[" | "]" | "^")+ } 

//-- letters and numbers with spaces, and no brackets
opts = { "[" ~ opts_group? ~ "]" }
opts_group = { (literal_group_opts | cmd_stmt)* }
literal_group_opts = { char+ } 

// command statement
//-- no newlines allowed in a cmd body
cmd_stmt = { ctrl_character ~ (!("begin"|"end") ~ name) ~ opts? ~ ("{" ~ ( cmd_stmt | literal_group | ("{" ~ ( cmd_stmt | literal_group )* ~ "}") )* ~ "}")* }

// environments
env_stmt            = { ctrl_character ~ "begin" ~ opts? ~ "{" ~ PUSH(env_name) ~ "}" ~ env_content ~ ctrl_character ~ "end" ~ "{" ~ POP ~ "}" }
env_content         = { (paragraph? ~ newline_char)* }
env_name            = { name }

// code block - to allow for { } characters and disallow nested command statements
code_stmt            = { ctrl_character ~ "begin" ~ opts? ~ "{" ~ PUSH("minted") ~ "}" ~  "{" ~ code_description ~ "}"~ code_content ~ ctrl_character ~ "end" ~ "{" ~ POP ~ "}" }
code_description     = { name }
code_content         = { ((literal_group_code)* ~ newline_char)* }
literal_group_code   = { ( char | "=" | (!"\\end{minted}" ~ "\\") | "^" | "[" | "]"  | "{" | "}" | apl_chars)+ } 
apl_chars = { "←" | "⊃" | "⍵" | "∨" | "∧" | "⌿" | "¯" |"∘" | "⊖" | "⌽" | "¨" | "⊂"}

// characters
name           = @{ (ASCII_ALPHANUMERIC | "^" | "$") + } //-- used for command and environment names
COMMENT        = _{ "%" ~ (!newline_char ~ ANY)* ~ newline_char }
WHITESPACE     = _{ ( " " | "\t" ) }
newline_char   = _{ "\n" }
ctrl_character = _{ "\\" }
char           = @{ ASCII_ALPHANUMERIC | punctuation | non_ascii }
punctuation    = {
    "," | "." | ";" | "'" | "`" | "’" | "\"" | "\\\\" | "!" | "*" | "+" |"?" | "/" | "(" | ")" | "|" | "<" | ">" | ":" | "=" | "-" | "_" | "—" | "~" | "&" | "@" | "$" | "#" | "\\#" | "\\&" | "\\%" | "\\_" | "\\{" | "\\}" | "“" | "”"
}
non_ascii      = { "é" | "è" | "à" | "ë" | "ç" | "ô" | "ü" | "Ü" | "ñ" | "ä" | "ō" | "τ" | "ε" | "χ" | "ν" | "η" | "Ā" | "ṭ" | "整" | "理" | "頓" | "清" | "掃" | "潔" | "躾"}