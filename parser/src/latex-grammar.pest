// Top level rule is `document`.
document = { 
    SOI ~
    (section? ~ newline_char)* ~ section? ~
    EOI
} //-- the last section? catches the last line

// section is representing a paragraph
section = {
    env_stmt |
    cmd_stmt |
    ( cmd_stmt | literal_group )*
}

literal_group      = { char+ } //-- letters and numbers with no spaces
char         = @{ ASCII_ALPHANUMERIC | punctuation }
punctuation  = {
    "," | "." | ";" | "(" | ")" | "[" | "]" | "|" | "<" | ">" | ":" | "=" | "-" | "_"
}

// Control Statement Grammar
//-- no newlines allowed in a cmd body
cmd_stmt = { ctrl_character ~ (!("begin"|"end") ~ name) ~ cmd_stmt_opt? ~ ("{" ~ ( cmd_stmt | literal_group )* ~ "}")* }
cmd_stmt_opt = { "[" ~ name? ~ "]" }


// Control Characters & Meta Characters
name                    = @{ ASCII_ALPHA+ } //-- used for command and environment names
COMMENT                 = _{ "%" ~ (!newline_char ~ ANY)* ~ newline_char }
WHITESPACE              = _{ " " }
newline_char            = _{"\n"}
ctrl_character       = _{ "\\" }

// Environment Grammar
env_stmt     = { ctrl_character ~ "begin" ~ env_stmt_opt? ~ "{" ~ PUSH(env_name) ~ "}" ~  ("{" ~ code_description ~ "}")? ~ env_content ~ ctrl_character ~ "end" ~ "{" ~ POP ~ "}" }
env_content  = { (section? ~ newline_char)* }
env_name = { name }
code_description = { name }
env_stmt_opt = { "[" ~ name ~ "]" }